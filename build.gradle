plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.financial.analytics'
version = '0.0.1-SNAPSHOT'
description = 'Spring Boot REST API for plain vanilla bond risk analytics'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	 implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation' // usually enough
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

}

tasks.named('test') {
	useJUnitPlatform()
}

jacoco {
	toolVersion = "0.8.10"
}

tasks.named('jacocoTestReport') {
	dependsOn tasks.test
	reports {
		xml.required.set(true)
		html.required.set(true)
	}
	executionData.setFrom(fileTree(buildDir).include("**/jacoco/*.exec", "**/*.exec"))
	sourceDirectories.setFrom(files("src/main/java"))
	classDirectories.setFrom(files("${buildDir}/classes/java/main"))
}

tasks.named('jacocoTestCoverageVerification') {
	dependsOn tasks.test
	dependsOn tasks.named('jacocoTestReport')
	executionData.setFrom(fileTree(buildDir).include('**/jacoco/*.exec', '**/*.exec'))
	sourceDirectories.setFrom(files('src/main/java'))
	classDirectories.setFrom(files("${buildDir}/classes/java/main"))
	violationRules {
		rule {
			limit {
				counter = 'INSTRUCTION'
				value = 'COVEREDRATIO'
				minimum = 0.90
			}
		}
	}
}

tasks.named('check') {
	dependsOn tasks.named('jacocoTestCoverageVerification')
}
